plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.21.4'
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

configurations {
}

def target_os = "linux"
if (System.properties['os.name'].toLowerCase(Locale.ROOT).contains('windows')) {
    target_os = "win"
} else if (System.properties['os.name'].toLowerCase(Locale.ROOT).contains('mac')) {
    target_os = "mac"
}

// overwrite target_os if you want to cross_compile

// Those JavaFX packages cause module conflicts between platforms and as such only one platform target can be built at a time
dependencies {
    implementation project(':backend')
    implementation group: 'org.openjfx', name: 'javafx-base', version: "14", classifier: target_os
    implementation group: 'org.openjfx', name: 'javafx-controls', version: "14", classifier: target_os
    implementation group: 'org.openjfx', name: 'javafx-fxml', version: "14", classifier: target_os
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: "14", classifier: target_os
}

mainClassName = 'listgengui.Launcher'

// Don't forget to download those jdks to the corresponding directories if you want to create jar files with jlink
def homePath = System.properties['user.home']

// As explained above, each JavaFX platform target causes conflicts with the others in modular java applications, so
// they have to be built one per jar.
jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    switch (target_os) {
        case "win":
            targetPlatform("win") {
                jdkHome = "${homePath}/Development/JDKS/openjdk-14.0.2_windows-x64_bin/jdk-14.0.2"
                addExtraModulePath("${homePath}/Development/JDKS/openjdk-14.0.2_windows-x64_bin/jdk-14.0.2/jmods")
            }
            break
        case "linux":
            targetPlatform('linux-x64', "${homePath}/Development/JDKS/openjdk-14.0.2_linux-x64_bin/jdk-14.0.2")
            break
        case "mac":
            targetPlatform('mac', "${homePath}/Development/JDKS/openjdk-14.0.2_osx-x64_bin/jdk-14.0.2")
            break
    }

    mergedModule {
        requires "java.xml.crypto"
        requires "java.logging"
        requires "java.xml"
        requires "java.desktop"
        requires "java.security.jgss"
        requires "jdk.charsets"
    }
    launcher {
        name = 'listgengui'
    }
}